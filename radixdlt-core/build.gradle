/*
 * (C) Copyright 2020 Radix DLT Ltd
 *
 * Radix DLT Ltd licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except in
 * compliance with the License.  You may obtain a copy of the
 * License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied.  See the License for the specific
 * language governing permissions and limitations under the License.
 */

plugins {
    id 'nebula.ospackage' version '8.4.1'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'me.champeau.gradle.jmh' version '0.5.1'
    id 'org.sonarqube' version '2.7.1'  /* Intentionally downgraded from 3.0 */
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.moowork.node' version '1.3.1'
    id 'net.nemerosa.versioning' version '2.14.0'
}

repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
}

def radixVersion() {
    def details = versionDetails()
    def version
    if (details.isCleanTag) {
        version = details.lastTag
    } else {
        version = details.branchName
        if (version == null) {
            version = "${details.lastTag}-detachedhead-${details.gitHash}"
        } else {
            // Prepend last tag so builddeb doesn't choke on non-numeric branch names
            version = "${details.lastTag}-${version}"
            version = version.replaceAll('/', '-')
        }
        version = "${version}-SNAPSHOT"
    }
    return version
}

group 'com.radixdlt'
version radixVersion()

subprojects {
    version = rootProject.version

    configurations.all {
        resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()
        }
    }

    apply plugin: 'java'

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Werror"
            options.compilerArgs << "-Xlint:-processing"    // Not really a useful warning
            options.compilerArgs << "-Xlint:-serial"        // Not using Java serialization
        }
    }

    repositories {
        jcenter()
        mavenCentral()
        /*
        maven {
            url 'https://maven.pkg.github.com/radixdlt/maven-packages'
            credentials {
                username = System.getenv("GPR_USER") ?: ''
                password = System.getenv("GPR_TOKEN") ?: ''
            }
        }
        */
        maven { url 'https://jitpack.io' }
    }

    compileJava {
        options.release = 11
    }

    task showRepos {
        doLast {
            println "All repos:"
            println repositories.collect { it.url }
        }
    }
}
