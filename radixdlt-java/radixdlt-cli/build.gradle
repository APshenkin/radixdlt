/*
 * (C) Copyright 2020 Radix DLT Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the “Software”),
 * to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS
 * OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
 * THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'org.sonarqube'
apply plugin: 'project-report'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'application'

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://jitpack.io' }
}

mainClassName = 'com.radixdlt.cli.RadixCLI'

compileJava {
    options.compilerArgs += '-proc:none'
}

compileTestJava {
    options.compilerArgs += '-proc:none'
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

checkstyle {
    configFile rootProject.file('.checkstyle.xml')
    toolVersion '8.10.1'
    showViolations = true
}

checkstyleTest {
    configFile rootProject.file('.checkstyle_test.xml')
}

test {
    systemProperty "java.security.egd", "file:/dev/urandom"
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

configurations.all {
    resolutionStrategy.eachDependency {
        if(it.requested.name == 'guava') {
            it.useTarget 'com.google.guava:guava:29.0-jre'
        }
        if(it.requested.name == 'json') {
            it.useTarget 'org.json:json:20180813'
        }
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'commons-cli:commons-cli:1.4'
    compile 'info.picocli:picocli:4.4.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.5.10'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'

    annotationProcessor 'info.picocli:picocli-codegen:4.4.0'
    compile project(":radixdlt-java")
}


shadowJar {
    manifest {
        attributes 'Main-Class': 'com.radixdlt.cli.RadixCLI'
    }
    archiveName = "radixdlt-cli-${classifier}.${extension}"

}